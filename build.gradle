buildscript {
    ext {
        // Maven Central
        group = 'moe.pine'
        version = '0.2.1'
        description = 'Cache interceptors for SpringFramework'
        websiteUrl = 'https://github.com/pine/spring-cache-interceptors'
        gitUrl = 'https://github.com/pine/spring-cache-interceptors.git'
        inceptionYear = '2019'

        // Dependencies
        junitVersion = '5.7.1'
        servletVersion = '4.0.1'
        springVersion = '5.3.0'
        mockitoVersion = '4.8.1'
    }
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.spotbugs.snom:spotbugs-gradle-plugin:5.0.3'
        classpath 'net.rdrei.android.buildtimetracker:gradle-plugin:0.11.0'
    }
}


// ----- Project ------------------------------------------

apply plugin: 'java-library'
apply plugin: 'idea'

group rootProject.ext.group
version rootProject.ext.version
description rootProject.ext.description

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Framework
    compileOnly "org.springframework:spring-webmvc:$springVersion"
    testImplementation "org.springframework:spring-webmvc:$springVersion"
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    // Java Servlet
    compileOnly "javax.servlet:javax.servlet-api:$servletVersion"
    testImplementation "javax.servlet:javax.servlet-api:$servletVersion"

    // JUnit
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    // Mockito
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation "org.mockito:mockito-junit-jupiter:$mockitoVersion"
}

test {
    useJUnitPlatform()
}


// ----- JaCoCo -------------------------------------------

apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.8'
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}


// ----- SpotBugs -----------------------------------------

apply plugin: 'com.github.spotbugs'

spotbugs {
    toolVersion = '4.7.3'
    excludeFilter = file("${project.rootDir}/findbugs-exclude.xml")
    spotbugsTest.enabled = false
}

spotbugsMain {
    reports {
        xml.required = false
        html.required = true
    }
}


// ----- Build Time Tracker -------------------------------

apply plugin: 'build-time-tracker'

buildtimetracker {
    reporters {
        summary {
            ordered true
            barstyle 'ascii'
        }
    }
}


// ----- Maven Central ------------------------------------

apply from: "${rootProject.projectDir}/gradle/sonatype.gradle"


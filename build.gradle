buildscript {
    ext {
        servletVersion = '4.0.1'
        springVersion = '5.1.6.RELEASE'
        mockitoVersion = '2.24.5'
    }
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.11'
        classpath 'net.rdrei.android.buildtimetracker:gradle-plugin:0.11.0'
    }
}


// ----- Project ------------------------------------------

apply plugin: 'java-library'
apply plugin: 'idea'

group 'moe.pine'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    jcenter()
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked'
    options.compilerArgs << '-Xlint:deprecation'
}

dependencies {
    implementation "org.springframework:spring-webmvc:$springVersion"
    compileOnly "javax.servlet:javax.servlet-api:$servletVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "javax.servlet:javax.servlet-api:$servletVersion"
}


// ----- JaCoCo ---------------------------------------

apply plugin: 'jacoco'

jacoco {
    toolVersion = '0.8.3'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}


// ----- SpotBugs -------------------------------------

apply plugin: 'com.github.spotbugs'

spotbugs {
    toolVersion = '3.1.8'
    excludeFilter = file("${project.rootDir}/findbugs-exclude.xml")
    spotbugsTest.enabled = false
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}


// ----- Build Time Tracker -------------------------------

apply plugin: 'build-time-tracker'

buildtimetracker {
    reporters {
        summary {
            ordered true
            barstyle 'ascii'
        }
    }
}
